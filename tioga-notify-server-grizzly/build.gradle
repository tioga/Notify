project(":tioga-notify-server-grizzly") {

  version = tiogaAppVersion

  dependencies {
    compile(project(":tioga-notify-engine"))

    compile(project(":tioga-notify-processor-push"))
    compile(project(":tioga-notify-processor-swing"))
    compile(project(":tioga-notify-processor-smtp"))
    compile(project(":tioga-notify-processor-logger"))
    compile(project(":tioga-notify-processor-slack"))

    compile("org.springframework:spring-aop:4.1.6.RELEASE") {force = true}
    compile("org.springframework:spring-core:4.1.6.RELEASE") {force = true}
    compile("org.springframework:spring-beans:4.1.6.RELEASE") {force = true}
    compile("org.springframework:spring-context:4.1.6.RELEASE") {force = true}
    compile("org.tiogasolutions.runners:tioga-runners-spring-grizzly:+")

     // required by Jersey-Spring's stupid attempt to check for a ServletContext
		 compile("javax.servlet:javax.servlet-api:3.1.0")
  }

  File windowsServerStartFile = file("$buildDir/scripts/notify-server-start.bat");
  task createWindowsStartScript << {
    windowsServerStartFile.parentFile.mkdirs()
    FileWriter writer = new FileWriter(windowsServerStartFile);
    writer.write("@echo off\ncls\n");
    writer.write("echo Starting application\n");
    writer.write("%JAVA_HOME%\\bin\\javaw.exe -jar ./lib/${project.name}-${project.version}.jar\n");
    writer.close()
  }

  File linuxServerStartFile = file("$buildDir/scripts/notify-server-start.sh");
  task createLinuxStartScript << {
    linuxServerStartFile.parentFile.mkdirs()
    FileWriter writer = new FileWriter(linuxServerStartFile);
    writer.write("clear\n");
    writer.write("cd \"\$(dirname \$(readlink -f \$0))\"\n");
    writer.write("nohup \$JAVA_HOME/bin/java -jar ./lib/tioga-notify-server-grizzly-1.1.12.jar > ./notify-server.log 2>&1 &\n");
    writer.close()
  }

  File linuxServerStopFile = file("$buildDir/scripts/notify-server-stop.sh");
  task createLinuxStopScript << {
    linuxServerStopFile.parentFile.mkdirs()
    FileWriter writer = new FileWriter(linuxServerStopFile);
    writer.write("clear\n");
    writer.write("cd \"\$(dirname \$(readlink -f \$0))\"\n");
    writer.write("nohup \$JAVA_HOME/bin/java -jar ./lib/tioga-notify-server-grizzly-1.1.12.jar -shutdown > ./notify-server-shutdown.log 2>&1 &\n");
    writer.close()
  }

  apply plugin:'distribution'

  artifacts {
    archives distZip
  }

  distributions {
    main {
      contents {
        into("runtime") {
          from {"../runtime"}
        }
        into("tioga-notify-admin-app") {
          from {"../tioga-notify-admin-app"}
        }
        into("lib") {
          from(jar)
          from(project.configurations.runtime)
        }
        from(windowsServerStartFile, createWindowsStartScript)
        from(linuxServerStartFile, createLinuxStartScript)
        from(linuxServerStopFile, createLinuxStopScript)
      }
    }
  }

  jar {
    manifest {
      attributes(
        "Implementation-Version" : "0.1",
        "Main-Class"             : "org.tiogasolutions.notify.server.grizzly.NotifyServer",
        "Class-Path"             : configurations.compile.collect { it.getName() }.join(' ')
      )
    }
  }

  apply(from: urlCache.get("https://raw.githubusercontent.com/tioga/build-scripts/master/build-config-deployer.gradle"))

  task importAdminApp << {
    File dest = file("${projectDir}/src/main/resources/org/tiogasolutions/notify/admin/app")
    if (dest.deleteDir() == false) throw new IOException("Unable to delete files: " + dest)

    copy {
      from "${projectDir}/../tioga-notify-admin-app"
      into dest
    }
  }
  build.dependsOn(importAdminApp)
}
